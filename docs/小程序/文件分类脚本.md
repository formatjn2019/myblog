文件分类脚本

以前练习bat的时候写的，因为有几十G的乱七八糟的书，采用关键字进行分类。
（现在书改用calibre进行统一存储了，这个脚本只能吃灰或者用来分类其它文件了）



将一下内容保存为.cmd文件，编码格式应该为gbk等中文编码（中文windows环境）

```shell
@echo off
:: 搜索目录
set SEARCH_DIR=.\temp
:: 目标目录
set AIM_DIR=.\temp
:: 重复文件目录
set DUPLICATE_FILE_DIR=.\duplicate
:: 重复文件去后缀目录
set DUPLICATE_FILE_REMAMEDIR=.\duplicateRename
:: 分类规则文件夹
set CLASSIFICIAN_RULES_DIR=.\rules
:: 目标文件夹
set CLASSIFICIANED_DIR=.\classified
:: 重复文件名添加的后缀
set DUPLICATE_FILE_SUFFIX=_c
:: 过于重复文件后缀
set DUPLICATE_FILE_DELETE_SUFFIX=%DUPLICATE_FILE_SUFFIX%%DUPLICATE_FILE_SUFFIX%%DUPLICATE_FILE_SUFFIX%
:: 分类规则文件后缀
set CLASSIFICIAN_RULES_SUFFIX=.rule




:: 防止路径出错，增加分隔符
set SEARCH_DIR=%SEARCH_DIR%\
set AIM_DIR=%AIM_DIR%\
set CLASSIFICIAN_RULES_DIR=%CLASSIFICIAN_RULES_DIR%\
set DUPLICATE_FILE_DIR=%DUPLICATE_FILE_DIR%\
set DUPLICATE_FILE_REMAMEDIR=%DUPLICATE_FILE_REMAMEDIR%\
set CLASSIFICIANED_DIR=%CLASSIFICIANED_DIR%\

:: 设置模式1 简单模式 2 分类模式
echo 1 简单模式 2 分类模式
set /p modle=请输入选项:
if "%modle%" == "1" (
    echo 1
    echo modle
    :: 搜索目录
    set SEARCH_DIR=.\
    :: 目标目录
    set AIM_DIR=.\
) else (
    :: 检查目录是否存在，若不存在，创建目录
    echo modle
    if not exist %SEARCH_DIR% (
        echo "建立搜索文件夹"
        md %SEARCH_DIR%
    )
    if not exist %DUPLICATE_FILE_DIR% (
        echo "建立重复文件夹"
        md %DUPLICATE_FILE_DIR%
    )
    if not exist %DUPLICATE_FILE_REMAMEDIR% (
        echo "建立重复去除文件夹"
        md %DUPLICATE_FILE_REMAMEDIR%
    )
    if not exist %CLASSIFICIAN_RULES_DIR% (
        echo "建立分类规则文件夹"
        md %CLASSIFICIAN_RULES_DIR%
    )
    if not exist %CLASSIFICIAN_RULES_DIR% (
        echo "建立分类文件夹"
        md %CLASSIFICIANED_DIR%
    )

    echo 请将所有文件放入 %SEARCH_DIR% 
    echo 分类文件夹为 %CLASSIFICIANED_DIR%
    echo 重复文件文件夹为 %DUPLICATE_FILE_DIR%
    echo 重复文件去除后缀文件夹为 %DUPLICATE_FILE_REMAMEDIR%
    echo 分类规则文件夹为 %CLASSIFICIAN_RULES_DIR%
    echo 规则文件后缀为 %CLASSIFICIAN_RULES_SUFFIX%  编码格式应为gb2312
    echo 初始化完成
    echo.
    pause

)
:: 菜单
:menu
    CLS
    color 03
    echo "1.  重命名并移动所有文件夹（去除文件夹名称中空格造成的问题）"
    echo "2.  移动所有文件到根目录"
    echo "3.  删除搜索目录下所有空文件夹"
    echo "4.  将目标目录下所有文件根据类型进行分类"
    echo "5.  顺序执行1,2,3,4"
    if "%modle%" == "2" (
        echo "6.  移动重复文件到重复文件文件夹"
        echo "7.  去除重复文件后缀(单次有效)"
        echo "8.  根据指定规则进行文件分类"
        echo "9.  将分类完成的文件按照文件类型再次分类"
        echo "10. 顺序执行2,6,7,8,9"
        echo "11. 删除所有文件夹"
        echo "12. 移动出错的文件名"
    )
    echo "0.  退出"
    set /p id=请输入选项:
    if "%id%"=="1" goto cmd1
    if "%id%"=="2" goto cmd2
    if "%id%"=="3" goto cmd3
    if "%id%"=="4" goto cmd4
    if "%id%"=="5" goto cmd5
    if "%modle%" == "2" (
        if "%id%"=="6" goto cmd6
        if "%id%"=="7" goto cmd7
        if "%id%"=="8" goto cmd8
        if "%id%"=="9" goto cmd9
        if "%id%"=="10" goto cmd10
        if "%id%"=="11" goto cmd11
        if "%id%"=="12" goto cmd12
    )
    
    if "%id%" == "0" (
        exit 0
    ) else (
        echo "输入错误 %id% 不是指定选项"
        pause
        goto menu 
    )

    
    :cmd1
    :: 将所有文件夹移动到根目录并重命名防止空格和括号访问造成错误
    call :moveDirs
    pause
    goto menu
    :cmd2
    :: 移动所有文件到目标目录
    call :traversalMoveRoot
    pause
    goto menu
    :cmd3 
    :: 遍历删除文件夹
    call :traversalDeleteEmptyDir %AIM_DIR%
    pause
    goto menu
    :cmd4 
    ::  根据后缀分类文件
    call :classifyFilesBySuffix
    pause
    goto menu
    :cmd5 
    :: 依次调用1234命令
    call :moveDirs
    call :traversalMoveRoot
    call :traversalDeleteEmptyDir %AIM_DIR%
    call :classifyFilesBySuffix
    pause
    goto menu
    :cmd6
    :: 将重复文件移动到重复文件存放目录
    call :duplicateFileMoveDuplicateDir
    pause
    goto menu
	:cmd7
    :: 去除根目录下重复文件的后缀
    call :renameduplicateFile
    pause 
    goto menu
    :cmd8
    :: 去除根目录下重复文件的后缀
    call :classifyFilesByRules
    pause 
    goto menu
    :cmd9
    :: 根据指定文件进行文件分类
    call :reClassifyFilesBySuffix
    pause
    goto menu
    :cmd10
    :: 去除根目录下重复文件的后缀
    call :traversalMoveRoot
    call :duplicateFileMoveDuplicateDir
    call :renameduplicateFile
    call :classifyFilesByRules
    call :reClassifyFilesBySuffix
    pause 
    goto menu
    :cmd11
	:: 删除空文件夹
    call :traversalDeleteDir %AIM_DIR%
    pause
    goto menu
    :cmd12
	:: 删除空文件夹
    call :traversalMoveErrorFileNameRoot
    pause
    goto menu
    
exit /B 0


:: 命令1
:: 重命名文件夹
:moveDirs
    setlocal 
    for  /d /r  %AIM_DIR% %%d in (*)  do (
        call :renameDir "%%d" "%AIM_DIR%" 
    )
    endlocal
exit /B 0

:: 生成新的文件夹名称并重命名 参数1 文件路径 参数2 目标目录
:renameDir
    set /a num=0 
    setlocal enabledelayedexpansion
        :increaseBigNum
        set /a num+=50
        :: 设置大步进，减少搜索次数
        if exist "%~2temp!num!" (
            echo "文件夹已存在，将重命名"
            goto increaseBigNum
        )
        set /a num-=50
        :increaseNum
        set /a num+=1
        echo !num!
        if  exist "%~2temp!num!" (
            echo "文件夹已存在，将重命名"
            goto increaseNum
        )

        move "%~1" "%~2temp!num!"
    endlocal
exit /B 0

:: 命令2
:: 将文件移动到根目录
:traversalMoveRoot
    setlocal
    for /R %SEARCH_DIR% %%f in (*) do ( 
        echo "正在移动文件 %%~f"
        call :moveFile "%%~f","%AIM_DIR%","%%~nf"
    )
    endlocal
exit /B 0

:: 移动文件 参数1 原文件全路径 参数2 目标路径 参数3 重命名名称 参数4 重复后缀删除
:moveFile
    setlocal
    echo "FILEPATH IS %~1 ; AIM IS %~2 ; FILERENAME IS %~3"
    set dir1=%~dp1
    set dir2=%~dp2
    set dir1=%dir1: =%
    set dir2=%dir2: =%
    echo "dir1 is "%dir1%
    echo "dir2 is "%dir2%
    :: 采用此方式 排除空白文件夹干扰
    if "%dir1%" equ "%dir2%" (
        echo "当前文件夹 不进行移动 %~1"
    ) else (
        if exist "%~dp2%~3%~x1" (
            echo "存在该文件文件"
            :: 删除重复次数过多的文件
            if exist "%~dp2%~3%DELETE_FILE_SUFFIX%%~x1" (
                del /S /Q "%~1"
            ) else (
                call :moveFile "%~1","%~2","%~3%DUPLICATE_FILE_SUFFIX%"
            )
                
            
        ) else (
            echo "不存在该文件"
            move "%~1" "%~dp2%~3%~x1"
            
        )
    )
    endlocal
exit /B 0

:: 命令3
:: 遍历删除空文件夹
:traversalDeleteEmptyDir
    echo %~dp1
    for /D %%d in ( %~dp1* ) do ( 
        echo "删除文件夹 %%d"
        rmdir "%%d"
    )
exit /B 0

:: 命令4
:: 按照类型分类文件 
:classifyFilesBySuffix
    setlocal
    for  %%f in (%SEARCH_DIR%*) do ( 
        echo "正在处理文件 %%f"
        if  "%%~dpnxf" == "%~dpnx0" (
            echo "当前批处理文件，不进行处理"
        ) else (
            call :makeDirBySuffix "%%f" "%AIM_DIR%" true
        )
    )
    endlocal
exit /b 0


:: 建立文件夹，根据类型进行移动 参数1  文件名 参数2 目标文件夹 参数3 是否进行移动
:makeDirBySuffix
    setlocal
    set fileSuffix=%~x1
    echo %fileSuffix%
    set fileSuffix=%fileSuffix:~1%
    echo %fileSuffix%
    set dirName=%~dpn2\%fileSuffix%
    echo %dirName%
    echo %~1
    if not exist %dirName% (
        md %dirName%
        echo 建立文件夹%dirName%
    )
    if %~3 == true (
        move "%~1" "%dirName%"
    )
    endlocal
exit /b 0



:: 命令6
:: 将重复文件移动到指定目标目录
:duplicateFileMoveDuplicateDir
    setlocal
    echo %AIM_DIR%
    echo %DUPLICATE_FILE_SUFFIX%
    for /R %AIM_DIR% %%f in (*%DUPLICATE_FILE_SUFFIX%.*) do ( 
        echo "正在移动文件 %%~f"
        call :moveFile "%%~f","%DUPLICATE_FILE_DIR%","%%~nf"
    )
    endlocal
exit /B 0

:: 命令7
:: 去除重复文件后缀
:renameduplicateFile
    setlocal enabledelayedexpansion
    :: 将所有文件重命名移动到指定目录
    for /R %DUPLICATE_FILE_DIR% %%f in (*%DUPLICATE_FILE_SUFFIX%.*) do ( 
        set newName=%%~nf
        echo %DUPLICATE_FILE_REMAMEDIR%
        call :moveFile "%%f","%DUPLICATE_FILE_REMAMEDIR%","!newName:%DUPLICATE_FILE_SUFFIX%=!"   
    )
    :: 将重复的文件移动回来
    for /R %DUPLICATE_FILE_REMAMEDIR% %%f in (*%DUPLICATE_FILE_SUFFIX%.*) do ( 
        call :moveFile "%%f","%DUPLICATE_FILE_DIR%","%%~nf"   
    )
    endlocal
exit /B 0

:: 命令8
:: 根据规则进行文件分类
:classifyFilesByRules
    setlocal enabledelayedexpansion
    for  %%f in (%CLASSIFICIAN_RULES_DIR%*%CLASSIFICIAN_RULES_SUFFIX%) do ( 
        echo "正在处理文件 %%f"
        set ruleFileName=%%~nf
        set ruleFileName=!ruleFileName:~3!
        echo !ruleFileName!
        for /F %%l in (%%f) do (
            if not exist %CLASSIFICIANED_DIR%!ruleFileName! (
                md  %CLASSIFICIANED_DIR%!ruleFileName!
            )
            echo 移动%%l
            move "%AIM_DIR%*%%l*" "%CLASSIFICIANED_DIR%!ruleFileName!"
        )
    )
    endlocal
exit /B 0

:: 命令9
:: 将分类好的文件再次按照类型分类 
:reClassifyFilesBySuffix
    setlocal
    for /D %%d in (%CLASSIFICIANED_DIR%*) do ( 
        echo "正在处理文件夹 %%d"
        for %%f in (%%d\*) do (
            echo 正在处理文件%%f
            call :makeDirBySuffix "%%f" "%%d" true
        )
    )
    endlocal
exit /B 0




:: 命令11
:: 遍历删除文件夹
:traversalDeleteDir
    echo %~dp1
    for /D %%d in ( %~dp1* ) do ( 
        echo "删除文件夹 %%d"
        rmdir /S /Q %%d
    )
exit /B 0

:: 移动出错文件名到根目录
:traversalMoveErrorFileNameRoot
    setlocal
    for /R %SEARCH_DIR% %%f in ("*%%*") do ( 
        echo "正在移动文件 %%~f"
        move %%f %AIM_DIR%
    )
    endlocal
exit /B 0
```



规则生成文件 需要python环境 保存为gbk格式的.cmd文件

```shell
del *.rule
python createRules.py
```

createRules.py 文件内容

三个数字，是按照顺序进行依次执行按关键字查找和移动，自动创建规则文件

```python
rules={
        "001书单":[
            "名称1",
            "关键字1",
        ],
        "045九州":[
        ]
    }

for name,keyValueList in rules.items():
    content="\n".join(keyValueList)
    with open("./" + name + ".rule", "w", encoding="gbk") as f:
        f.write(content)



def writeFile(name,content):
    with open ("./"+name+".rule","w",encoding="gbk") as f:
        f.write(content)

```

